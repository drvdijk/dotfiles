# .zshrc is sourced in interactive shells.

export LC_CTYPE=en_US.UTF-8
export LC_ALL=en_US.UTF-8

export DOTFILES=$HOME/.dotfiles


# Load zplug
source $(dirname $(readlink ${(%):-%N}))/zplug.sh

# delete/home/end keys don't function properly ...
bindkey "^[[3~" delete-char
bindkey "\033[1~" beginning-of-line
bindkey "\033[4~" end-of-line


#
# Zsh submodules
#

# All zsh config files (all *.zsh files contained in $DOTFILES children
# directories). Note that this glob is limited to one directory level deep.
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# Load path files
for file in ${(M)config_files:#*/path.zsh}; do
  source $file
done

# Load all files except for path and completions
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source $file
done

# Load completions
for file in ${(M)config_files:#*/completion.zsh}; do
  source $file
done

# Cleanup
unset config_files


# Load tmux
#[[ $SHLVL == "1" ]] && tmux new
